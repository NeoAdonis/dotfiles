# zsh and oh-my-zsh options
DISABLE_AUTO_TITLE="true"
plugins=(archlinux git rbw zoxide zsh-autosuggestions fast-syntax-highlighting history-substring-search)
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_DUPS
setopt HIST_REDUCE_BLANKS
# End of zsh and oh-my-zsh options

# WSL check
if [[ -f /proc/sys/fs/binfmt_misc/WSLInterop ]]; then
	export WSL=1
fi
# End of WSL check

# oh-my-zsh
ZSH=/usr/share/oh-my-zsh
ZSH_CUSTOM=/usr/share/zsh
ZSH_CACHE_DIR="$HOME/.cache/oh-my-zsh"
if [[ ! -d $ZSH_CACHE_DIR ]]; then
	mkdir $ZSH_CACHE_DIR
fi
ZSH_COMPDUMP="$ZSH_CACHE_DIR/.zcompdump"
source $ZSH/oh-my-zsh.sh
# End of oh-my-zsh

# oh-my-posh
eval "$(oh-my-posh init zsh --config $HOME/.omp.json)"
# End of oh-my-posh

# fast-syntax-highlighting
FSH_THEME="catppuccin-mocha"
if ! fast-theme -s | grep -q "$FSH_THEME"; then
	fast-theme "XDG:$FSH_THEME"
fi
# End of fast-syntax-highlighting

# fzf
source <(fzf --zsh)
# End of fzf

# fzf yay helpers
# https://github.com/junegunn/fzf/wiki/examples#pacman
yzf() {
	pos=$1
	shift
	sed "s/ /\t/g" |
		fzf --nth=$pos --multi \
			--preview-window=60%,border-left \
			--bind="double-click:execute(xdg-open 'https://archlinux.org/packages/{$pos}'),alt-enter:execute(xdg-open 'https://aur.archlinux.org/packages?K={$pos}&SB=p&SO=d&PP=100')" \
			"$@" | cut -f$pos | xargs
}

yas() {
	if ! command -v yay >/dev/null 2>&1; then
		echo "error: yay is not installed." >&2
		return 1
	fi
	cache_dir="/tmp/yas-$USER"
	test "$1" = "-y" && rm -rf "$cache_dir" && shift
	mkdir -p "$cache_dir"
	preview_cache="$cache_dir/preview_{2}"
	list_cache="$cache_dir/list"
	{ test "$(cat "$list_cache$@" | wc -l)" -lt 50000 && rm "$list_cache$@"; } 2>/dev/null
	pkg=$( (cat "$list_cache$@" 2>/dev/null || {
		pacman -Sl --color always "$@"
		yay -Sl aur --color always "$@"
	} | sed 's/ [^ ]*unknown-version[^ ]*//' | tee "$list_cache$@") | yzf 2 --ansi --tiebreak=index --preview="cat $preview_cache 2>/dev/null | grep -v 'Querying' | grep . || yay --color always -Si {2} | tee $preview_cache")
	if test -n "$pkg"; then
		echo "Installing $pkg..."
		cmd="yay -S $pkg"
		print -s "$cmd"
		eval "$cmd"
		rehash
	fi
}

yar() {
	if ! command -v yay >/dev/null 2>&1; then
		echo "error: yay is not installed." >&2
		return 1
	fi
	pkg=$(yay -Q --color always "$@" | yzf 1 --ansi --tiebreak=length --preview="yay --color always -Qli {1}")
	if test -n "$pkg"; then
		echo "Removing $pkg..."
		cmd="yay -Rcs $pkg"
		print -s "$cmd"
		eval "$cmd"
	fi
}
# End of fzf yay helpers

# yazi
y() {
	if ! command -v yazi >/dev/null 2>&1; then
		echo "error: yazi is not installed." >&2
		return 1
	fi
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
	local start end duration count=0
	# work-around: yazi might fail to start when run using SSH on Windows
	while [ "$count" -lt 10 ]; do
		start=$(date +%s%3N)
		yazi "$@" --cwd-file="$tmp" </dev/null
		end=$(date +%s%3N)
		duration=$((end - start))
		count=$((count + 1))
		# run again unless it runs for at least a short while
		if [ "$duration" -ge 1000 ]; then
			break
		fi
	done
	IFS= read -r -d '' cwd <"$tmp"
	[ -n "$cwd" ] && [ "$cwd" != "$PWD" ] && builtin cd -- "$cwd"
	rm -f -- "$tmp"
}
# End of yazi

# vars
alias cls=clear
alias ls=lsd
alias pip="uv pip"
export BAT_THEME="Catppuccin Mocha"
export MANPAGER="sh -c 'awk '\''{ gsub(/\x1B\[[0-9;]*m/, \"\", \$0); gsub(/.\x08/, \"\", \$0); print }'\'' | bat -p -lman'"
export MICRO_TRUECOLOR=1
export XDG_CACHE_HOME="$HOME/.cache"
export XDG_CONFIG_HOME="$HOME/.config"
export XDG_DATA_HOME="$HOME/.local/share"
export XDG_STATE_HOME="$HOME/.local/state"
[ -n "$DISPLAY" ] && export EDITOR="code -w" || export EDITOR=micro
[ -n "$DISPLAY" ] && [ -z "$WSL" ] && export DIFFPROG=kompare || export DIFFPROG=delta
[ -n "$DISPLAY" ] && [ -z "$WSL" ] && export MERGEPROG=meld || export DIFFPROG=diff3
[ -n "$DISPLAY" ] && [ -z "$WSL" ] && export BROWSER=firefox || export BROWSER=""
export VISUAL=$EDITOR
# End of vars
